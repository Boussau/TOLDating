###############################################################
######################### INFERENCE:###########################
###############################################################


times_fn = "TestData/timeBoundaries.txt"
levels_fn = "TestData/pO2_levels.txt"
aero_data <- readDiscreteCharacterData("TestData/simulated_Data.nex")

n_sites = 2000
taxa = aero_data.taxa()
n_taxa = taxa.size()
n_branches = 2 * n_taxa - 2

#####################
### Tree model
#####################
root_age ~ dnUniform(3.8, 4.5)

rho <- 1/50 # proportion of sampled taxa

birth ~ dnExp(10) # birth prior
death ~ dnExp(10) # death prior

tree ~ dnBDP(lambda=birth, mu=death, rho=rho, rootAge=root_age, taxa=taxa)

topo <- readTrees ("TestData/simulated_Tree.nex")[1]
tree.setValue(topo)
root_age.setValue(topo.rootAge()) # We set the root age to the truth


#####################
### Epochs and their bounds
#####################
time_bounds <- readDataDelimitedFile(file=times_fn, delimiter=" ")
levels <- readDataDelimitedFile(file=levels_fn, delimiter=" ")
n_epochs <- time_bounds.size()
for (i in 1:n_epochs) {
  epoch_times[i] <- time_bounds[i][1]
  epoch_levels[i] <- levels[i][1]
}

#####################
### Epoch-specific models of aerobiosis evolution
#####################


# We use a logistic function to specify the probability of being aerobe based on O2 partial pressures at a given time point:
# f(x) = L/(1+exp(-k*(x-x0)))
# We have : L: the maximum value, i.e. 1.0
# k: the steepness of the curve
# x0: the sigmoid's midpoint

L <- 1.0
k ~ dnExponential(1)
x0~ dnUniform(0, 100)

for (i in 1:n_epochs) {
  pi_aerobe[i] := L/(1+exp(-k*(epoch_levels[i]-x0)))
  pi_anaerobe[i] := abs(1-pi_aerobe[i])
  Q_aero[i] := fnF81(Simplex(pi_aerobe[i], pi_anaerobe[i]))
}


#####################
### Complete model of aerobiosis evolution:
#####################
pi_aerobe_root := pi_aerobe[1]
pi_anaerobe_root := abs(1-pi_aerobe_root)
root_frequency := Simplex(pi_aerobe_root, pi_anaerobe_root)
Q_aero_epoch := fnEpoch(Q=Q_aero, times=epoch_times, rates=rep(1.0,n_epochs))

Q_aero_epoch # printing the model on stdout

# Rate of evolution for the molecular clock
branchRates ~ dnExponential(10)

#####################
### PhyloCTMC:
#####################
evo_aero ~ dnPhyloCTMC(tree=tree, rootFrequencies=root_frequency, Q=Q_aero_epoch, branchRates=branchRates, type="Standard", nSites=n_sites)

#####################
### Clamping to the simulated data:
#####################
evo_aero.clamp(aero_data)

#####################
### Moves:
#####################

moves = VectorMoves()
moves.append( mvScale(birth, weight=2) )
moves.append( mvScale(death, weight=2) )
moves.append( mvNodeTimeSlideUniform(tree, weight=n_branches/2) )
moves.append( mvSubtreeScale(tree, weight=n_branches/8) )
moves.append( mvScale(k, weight=10) )
moves.append( mvScale(x0, weight=10) )
moves.append( mvScale(branchRates, weight=10) )

# The root age is fixed:
# moves.append( mvTreeScale(tree, root_age, weight=n_branches/8) )
# moves.append( mvScale(root_age, weight=5) )

#####################
### Launch the analysis:
#####################
# initialize the model object #
mymodel = model(tree)

# Create a vector of monitors #
monitors = VectorMonitors()
# 1. for the full model #
monitors.append( mnModel(filename="output/correct_model.log", printgen=10) )
# 2. the tree #
monitors.append( mnFile(filename="output/correct_model.trees", printgen=10, tree) )
# 3. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=100) )

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #
mymcmc.run(generations=20000, tuningInterval=200)

# check the performance of the MCMC/moves
mymcmc.operatorSummary()

# Read in the tree trace and construct the consensus tree tree #
trace = readTreeTrace("output/correct_model.trees", treetype="clock")
trace.setBurnin(0.25)

# Summarize tree trace and the consensus tree to file
mapTree(trace, file="output/correct_model_map.tre")
consensusTree(trace, file="output/correct_model_majrule.tre")

# run 1:
trace = readTreeTrace("output/correct_model_run_1.trees", treetype="clock")
trace.setBurnin(0.25)
mapTree(trace, file="output/correct_model_run_1_map.tre")

# run 2:
trace = readTreeTrace("output/correct_model_run_2.trees", treetype="clock")
trace.setBurnin(0.25)
mapTree(trace, file="output/correct_model_run_2_map.tre")


# Quit RevBayes #
q()
